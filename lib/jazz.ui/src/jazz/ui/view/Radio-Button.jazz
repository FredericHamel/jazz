;;;==============
;;;  JazzScheme
;;;==============
;;;
;;;; Radio Buttons
;;;
;;;  The contents of this file are subject to the Mozilla Public License Version
;;;  1.1 (the "License"); you may not use this file except in compliance with
;;;  the License. You may obtain a copy of the License at
;;;  http://www.mozilla.org/MPL/
;;;
;;;  Software distributed under the License is distributed on an "AS IS" basis,
;;;  WITHOUT WARRANTY OF ANY KIND, either express or implied. See the License
;;;  for the specific language governing rights and limitations under the
;;;  License.
;;;
;;;  The Original Code is JazzScheme.
;;;
;;;  The Initial Developer of the Original Code is Guillaume Cartier.
;;;  Portions created by the Initial Developer are Copyright (C) 1996-2008
;;;  the Initial Developer. All Rights Reserved.
;;;
;;;  Contributor(s):
;;;
;;;  Alternatively, the contents of this file may be used under the terms of
;;;  the GNU General Public License Version 2 or later (the "GPL"), in which
;;;  case the provisions of the GPL are applicable instead of those above. If
;;;  you wish to allow use of your version of this file only under the terms of
;;;  the GPL, and not to allow others to use your version of this file under the
;;;  terms of the MPL, indicate your decision by deleting the provisions above
;;;  and replace them with the notice and other provisions required by the GPL.
;;;  If you do not delete the provisions above, a recipient may use your version
;;;  of this file under the terms of any one of the MPL or the GPL.
;;;
;;;  See www.jazzscheme.org for details.


(module protected jazz.ui.view.Radio-Button jazz


(import (jazz.graphic)
        (jazz.jml)
        (jazz.locale)
        (jazz.system)
        (jazz.ui)
        (jazz.ui.window))


(class Radio-Button extends View implements (Groupable Selectable)
  
  
  (definition box-size
    13)
  
  
  (property group     initialize #f accessors explicit)
  (property selected? initialize #f accessors explicit)
    
  
  (slot tracking? initialize #f)
  (slot inside?   initialize #f)
  
  
  (method override (default-size)
    {Dimension 100 16})

  
  ;;;
  ;;;; Groupable
  ;;;


  (method override (get-group)
    group)


  (method override (set-group value)
    (set! group value))

  
  ;;;
  ;;;; Selectable
  ;;;


  (method override (get-selected?)
    selected?)
  

  (method override (set-selected? flag)
    (when (neq? flag selected?)
      (let ((current (get-current-selection)))
        (set-selected-state flag)
        (when current
          (set-selected-state~ current (not flag))))))
  
  
  (method override (unselect)
    (when selected?
      (set-selected-state #f)))
  
  
  (method (get-current-selection)
    (continuation-capture
      (lambda (return)
        (for-each-button
          (lambda (button)
            (when (get-selected?~ button)
              (continuation-return return button))))
        #f)))
  
  
  (method (for-each-button proc)
    (for-each (lambda (child)
                (when (and (is? child Radio-Button)
                           (eqv? group (get-group~ child)))
                  (proc child)))
              (get-children~ parent)))
  
  
  (method (set-selected-state flag)
    (set! selected? flag)
    (invalidate-view))
  
  
  ;;;
  ;;;; Drawing
  ;;;
  
  
  (method override (draw surface context lh lv)
    (let* ((title (or (localize title) ""))
           (v (quotient (- (get-height) (if (or (not title) (empty-string? title))
                                            13
                                            (get-height~ (get-text-extent~ surface title (effective-font)))))
                        2))
           (rect (new Rect 0 (+ v 1) box-size (+ v box-size 1)))
           (x (+ (get-left~ rect) (/ (- (get-right~ rect) (get-left~ rect)) 2.)))
           (y (+ (get-top~ rect) (/ (- (get-bottom~ rect) (get-top~ rect)) 2.)))
           (r (/ (- (get-right~ rect) (get-left~ rect)) 2.))
           (M_PI 3.14159265358979323846))
      (arc~ surface x y r 0 (* 2 M_PI))
      (set-color~ surface (if (or (not enabled?)
                                  (and tracking? inside?))
                              {Color Light-Background}
                              {Color White}))
      (fill~ surface)
      (set-line-width~ surface 1)
      (arc~ surface x y (exact->inexact r) (* 3 (/ M_PI 4)) (* 7 (/ M_PI 4)))
      (set-color~ surface {Color Draw-Edge-Medium})
      (stroke~ surface)
      (arc~ surface x y (exact->inexact (- r 1)) (* 3 (/ M_PI 4)) (* 7 (/ M_PI 4)))
      (set-color~ surface {Color Draw-Edge-Dark})
      (stroke~ surface)
      (arc~ surface x y (exact->inexact r) (* 7 (/ M_PI 4)) (* 3 (/ M_PI 4)))
      (set-color~ surface {Color White})
      (stroke~ surface)
      (arc~ surface x y (exact->inexact (- r 1)) (* 7 (/ M_PI 4)) (* 3 (/ M_PI 4)))
      (set-color~ surface {Color Draw-Edge-Light})
      (stroke~ surface)
      (when selected?
        (arc~ surface x y (exact->inexact (/ r 3)) (cast <fl> 0) (* 2 M_PI))
        (set-color~ surface {Color Black})
        (fill~ surface))
      (draw-text~ surface 18 v title (effective-font) {Color Black})))
  
  
  ;;;
  ;;;; Mouse
  ;;;
  
  
  (method override (mouse-down pos)
    (when (view-enabled?)
      (set! tracking? #t)
      (set! inside? #t)
      (acquire-capture)
      (invalidate-view)))
  
  
  (method override (drag-move pos)
    (when tracking?
      (let ((over? (over-view? pos)))
        (when (neq? over? inside?)
          (set! inside? over?)
          (invalidate-view)))))
  
  
  (method override (drag-up pos . rest)
    (when tracking?
      (release-capture)
      (set! tracking? #f)
      (when inside?
        (set-selected? (not selected?))
        (set-modified? #t)
        (process-action self))))
  
  
  ;;;
  ;;;; Descriptor
  ;;;
  
  
  (method meta override (get-class-descriptor)
    (new Radio-Button-Descriptor)))


;;;
;;;; Design
;;;


(submodule design


(import (jazz.designer))


(class Radio-Button-Descriptor extends View-Descriptor
  
  
  (form
    (<install>
      (<properties~>           default: title
        (<Property-Descriptor> name: group title: "Group")
        (<Property-Descriptor> name: selected? title: "Selected")))))))
